(import [stdlib base])
(import [stdlib test])

(defn recursive-reverse #{T} ([lst (List & T)]) -> (List & T)
  (if (nil? lst)
    lst
    (concat (recursive-reverse (rest lst)) (list (first lst)))))

(defn even-length? ([l (List & Any)]) -> Bool
  (if (nil? l)
    true
    (let [tail (rest l)]
      (if (nil? tail)
        false
        (recur (rest tail))))))

; This has a polymorphic purity
(defn recursive-every? #{T [->_ ->!]} ([pred (T ->_ Bool)] [l (List & T)]) ->_ Bool
  (if (nil? l)
    true
    (if (pred (first l))
      (recur pred (rest l))
      false)))

(defn main! () ->! ()
  (assert-eq! '() ((black-box! recursive-reverse) '()))
  (assert-eq! '(()) ((black-box! recursive-reverse) (black-box! '(()))))
  (assert-eq! '("three" "two" "one") (recursive-reverse (black-box! '("one" "two" "three"))))
  (assert-eq! '(7 6 5 4 3 2 1) (recursive-reverse '(1 2 3 4 5 6 7)))

  (assert-eq! true (even-length? '()))
  (assert-eq! false (even-length? (black-box! '(1))))
  (assert-eq! true ((black-box! even-length?) '(1 2)))
  (assert-eq! false ((black-box! even-length?) (black-box! '(1 2 3))))
  (assert-eq! true (even-length? '(1 2 3 4)))

  (assert-eq! true (recursive-every? int? '(1 2 3)))
  (assert-eq! false (recursive-every? int? '(1 2.0 3)))
  (assert-eq! true ((black-box! recursive-every?) int? '(1 2 3)))
  (assert-eq! false ((black-box! recursive-every?) int? '(1 2.0 3)))
  (assert-eq! true (recursive-every? (black-box! int?) '(1 2 3)))
  (assert-eq! false (recursive-every? (black-box! int?) '(1 2.0 3)))
  (assert-eq! true (recursive-every? int? (black-box! '(1 2 3))))
  (assert-eq! false (recursive-every? int? (black-box! '(1 2.0 3))))

  ; We should select an ABI for `even-length?` which doesn't require allocaton
  (let [black-box-list (black-box! '(1 2 3 4 5))
        (alloc-count is-even) (heap-alloc-count (fn () (even-length? black-box-list)))]
    (assert-eq! 0 alloc-count)
    (assert-eq! false is-even))

  ())
